begin model
begin parameters
  ksp 0.612e8 # 1/M 1/s 
  ksm 2.32e3
  kap 3.82e6
  kam 13
  b 0.25
  gamma 2e3
  delta 0.417e-3 # spontaneous release rate
  a 0.025
  k_dock 0.2
  Ca0 1e-7
end parameters

begin molecule types
  snare(s,s,s,s,s,a,a,dv~0~1)
  Ca(b)
  V_release()
end molecule types

begin species
  snare(s,s,s,s,s,a,a,dv~1) 7000
  # fixed calcium concentration
  $Ca(b) Ca0
end species

begin observables
  Molecules V_release V_release()
  Molecules sync_act snare(s!+,s!+,s!+,s!+,s!+)
  Molecules async_act snare(a!+,a!+)
  Molecules ns snare(s!+)
  Molecules na snare(a!+)
end observables

begin functions
end functions

begin reaction rules
  %x:snare(s) + Ca(b) <-> %x:snare(s!1).Ca(b!1) ksp, b^(ns(x)-1)*ksm
  %x:snare(a) + Ca(b) <-> %x:snare(a!1).Ca(b!1) kap, b^(na(x)-1)*kam

  # Vesicle release
  # spontaneous
  snare(dv~1) -> snare(dv~0) + V_release() delta
  #synchronous
  snare(s!+,s!+,s!+,s!+,s!+,dv~1) -> snare(s!+,s!+,s!+,s!+,s!+,dv~0) + V_release() gamma
  #asynchronous
  snare(a!+,a!+,dv~1) -> snare(a!+,a!+,dv~0) + V_release() a*gamma

  # vesicle docking to snare
  snare(dv~0) -> snare(dv~1) k_dock

end reaction rules

end model

generate_network({overwrite=>1})
simulate({method=>"ssa",t_end=>100,n_steps=>10000})